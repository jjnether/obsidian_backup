# SOURCE TRANSFER OPEN / CLOSE LOGIC
#
# SOURCE 1 INITIAL TRANSFER AND RETURN TRANSFER QUALIFIER
ACT14PU := AMV001 # SOURCE 1 IT TIME
ACT14DO := 0.000000
ACT14IN := ASV106 AND ASV117 AND NOT ASV002 AND ASV101 AND ASV112 AND ASV055 # S1 IT TIMING
ASV236 := ACT14Q AND ASV117 # S1 IT QUALIFIER
ACT15PU := AMV002 # SOURCE 1 RT TIME
ACT15DO := 0.000000
ACT15IN := ASV107 AND ASV001 AND ASV102 AND NOT ALT07 AND ASV055 AND NOT ALT15 # SOURCE 1 RT TIMING
ASV238 := R_TRIG ALT15 OR R_TRIG ACT15Q OR (ASV238 AND NOT (ACT09Q OR PCT12Q)) # SOURCE 1 RT QUALIFIER
#
#
# SOURCE 2 INITIAL TRANSFER AND RETURN TRANSFER QUALIFIERS
ACT16PU := AMV003 # SOURCE 2 IT TIME
ACT16DO := 0.000000
ACT16IN := ASV116 AND ASV107 AND NOT ASV001 AND ASV111 AND ASV102 AND ASV055 # SOURCE 2 IT QUALIFIER
ASV239 := ACT16Q AND ASV107 # START SOURCE 2 IT
ACT17PU := AMV004 # SOURCE 2 RT TIME
ACT17DO := 0.000000
ACT17IN := ASV117 AND ASV002 AND ASV112 AND NOT ALT08 AND ASV055 AND NOT ALT16 # SOURCE 2 RT QUALIFIER
ASV241 := R_TRIG ALT16 OR R_TRIG ACT17Q OR (ASV241 AND NOT (ACT08Q OR PCT12Q)) # SOURCE 2 RT
#
#
# SOURCE 1 IT AND RT CLOSE LOGIC
ASV197 := ALT08 AND ASV102 AND ASV112 # SOURCE 1 ITS CLOSE
ASV198 := (ASV238 AND (ASV011 AND NOT ALT15)) OR (NOT ASV011 OR ALT15) AND ASV112 AND F_TRIG ACT09Q # SOURCE 1 RTS CLOSE
ASV199 := (ASV197 OR ASV198 OR ACT23Q) OR AST08Q # AND AST07Q # SOURCE 1 AUTO CLOSE FROM ITS, RTS, EMERGENCY, AND TRANSFER FAIL
#
#
# SOURCE 2 IT AND RT CLOSE LOGIC
ASV206 := ALT07 AND ASV102 AND ASV112 # SOURCE 2 ITS CLOSE
ASV207 := (ASV241 AND (ASV011 AND NOT ALT16)) OR (NOT ASV011 OR ALT16) AND ASV102 AND F_TRIG ACT08Q # SOURCE 2 RTS CLOSE
ASV208 := (ASV206 OR ASV207 OR ACT24Q) OR AST09Q # AND AST07Q # SOURCE 2 AUTO CLOSE FROM ITS, RTS, EMERGENCY, AND TRANSFER FAIL
#
#
# SOURCE 1 IT AND RT OPEN LOGIC
ACT08PU := 0.000000 # 0 SEC PU TIME
ACT08DO := AMV005 # DO TIME
ACT08IN := (R_TRIG ASV111 OR R_TRIG ASV102) AND ASV241 # RTID FOR S2 RT
ASV209 := (F_TRIG ACT08Q AND ASV111 AND (ASV011 AND NOT ALT16)) OR (ASV241 AND (NOT ASV011 OR ALT16)) # SOURCE 1 OPEN COMMAND FOR S2 RT
ALT07S := R_TRIG ASV236 # SOURCE 1 IT
ALT07R := F_TRIG ASV199 OR (R_TRIG ASV111 AND ASV102) OR F_TRIG ALT02 OR F_TRIG ALT01 OR PSV01
ASV211 := ALT07 AND ASV101 AND ASV112 # SOURCE 1 IT OPEN QUALIFIER
ASV212 := ASV209 OR ASV211 # SOURCE 1 OPEN COMMAND
#
#
# SOURCE 2 IT AND RT OPEN LOGIC
ACT09PU := 0.000000 # 0 SEC PU TIME
ACT09DO := AMV005 # DO TIME
ACT09IN := (R_TRIG ASV101 OR R_TRIG ASV112) AND ASV238 # RTID FOR S1 RT
ASV216 := (F_TRIG ACT09Q AND ASV101 AND (ASV011 AND NOT ALT15)) OR (ASV238 AND (NOT ASV011 OR ALT15)) # SOURCE 2 OPEN COMMAND FOR S1 RT
ALT08S := R_TRIG ASV239 # SOURCE 2 IT
ALT08R := F_TRIG ASV208 OR R_TRIG ASV101 OR R_TRIG ALT14 OR F_TRIG ALT02 OR F_TRIG ALT01 OR PSV01
ASV217 := ALT08 AND ASV102 AND ASV111 # SOURCE 2 ITS OPEN COMMAND
ASV222 := ASV216 OR ASV217 # SOURCE 2 OPEN COMMAND
#
#
# SOURCE 1 OPEN / CLOSE LOGIC
ASV229 := NOT ASV141 AND (ASV112 OR ASV005) AND (ASV058 OR (R_TRIG ASV199 AND (NOT ASV011 OR ASV197)) OR (PSV40 AND ASV011)) # SOURCE 1 CLOSE
ASV230 := NOT ASV141 AND (NOT ALT08 OR PCT12Q) AND (ASV059 OR R_TRIG PCT12Q OR R_TRIG ASV212) # SOURCE 1 OPEN
ASV231 := (ASV231 OR ASV229) AND NOT ASV101 AND ASV102 AND NOT ACT10Q AND NOT IN201 AND NOT AST16Q # SOURCE 1 CLOSE VAR2
ASV232 := ASV230 AND ASV101 AND NOT ASV102 AND NOT ACT10Q AND NOT IN201 AND NOT AST16Q # SOURCE 1 OPEN VAR2
#
#
# SOURCE 2 OPEN / CLOSE LOGIC
ASV235 := NOT ASV141 AND (ASV102 OR ASV005) AND (ASV060 OR (R_TRIG ASV208 AND (NOT ASV011 OR ASV206)) OR (PSV41 AND ASV011)) # SOURCE 2 CLOSE
ASV242 := (NOT ALT07 OR PCT12Q) AND ((NOT ASV141 AND NOT ALT02 AND ASV061) OR (NOT ASV141 AND ASV055 AND (R_TRIG PCT12Q AND ASV111 OR R_TRIG ASV222))) # SOURCE 2 OPEN
ASV243 := (ASV243 OR ASV235) AND NOT IN202 AND NOT ASV111 AND ASV112 AND NOT ACT11Q AND NOT AST16Q # SOURCE 2 CLOSE VAR2
ASV244 := (ASV242 OR ASV244) AND ASV111 AND NOT ASV112 AND NOT ACT11Q AND NOT IN202 AND NOT AST16Q # SOURCE 2 OPEN VAR2 (THIS WILL OPEN WHEN RELAY CYCLES ENABLED!!!!!!!!!!)
#
#
# OPEN BOTH SOURCE WAYS ON LOSS OF VOLTAGE
ALT26S := PCT12Q # OBW CHECK FOR EMERGENCY CLOSE
ALT26R := (ASV107 AND ASV101) OR (ASV117 AND ASV111) OR NOT ASV019 OR NOT ASV055
ACT23PU := AMV002
ACT23DO := 0.000000
ACT23IN := NOT ACT24Q AND ASV076 # CLOSE S1 AFTER OBW
ASV076 := (ASV019 AND ASV107 AND ASV102 AND ASV112 AND ASV055) AND (ASV003 OR (NOT ASV117 AND ASV002) OR ASV001) # STARTS TIMER FOR CLOSE S1 AFTER OBW
ACT24PU := AMV004
ACT24DO := 0.000000
ACT24IN := NOT ACT23Q AND ASV077 # CLOSE S2 AFTER OBW
ASV077 := (ASV019 AND ASV117 AND ASV112 AND ASV102 AND ASV055) AND (ASV003 OR (NOT ASV107 AND ASV001) OR ASV002) # STARTS TIMER FOR CLOSE S2 AFTER OBW
ASV014 := PSV01 AND NOT PCT12Q AND ASV055 AND ASV019 AND NOT (ASV102 AND ASV112) # OPEN BOTH WAYS DISPLAY POINT
ASV036 := ASV019 AND (NOT ACT24Q AND (ASV019 AND ASV107 AND ASV102 AND ASV112 AND ASV055) AND (ASV003 OR (NOT ASV117 AND ASV002) OR ASV001) AND NOT ACT23Q) # USED IN S1_RT DP AND IS TRIGGERED IN OBW
ASV037 := ASV019 AND (NOT ACT23Q AND (ASV019 AND ASV117 AND ASV112 AND ASV102 AND ASV055) AND (ASV003 OR (NOT ASV107 AND ASV001) OR ASV002) AND NOT ACT24Q) # USED IN S2_RT DP AND IS TRIGGERED IN OBW
#













